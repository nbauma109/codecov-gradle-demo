apply plugin: 'java'
apply plugin: 'maven'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.8.14'
    }

    buildDir = rootDir.canonicalPath + "/build/" + rootProject.relativePath(projectDir.canonicalPath)

    version = '1.0.0'

    group 'com.example.sayhello'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.13.2'
    }
}

subprojects {
    apply plugin: 'maven'

    tasks.withType(Test) {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            html.enabled true
            csv.enabled false
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn subprojects.test, subprojects.classes

    doFirst {
        classDirectories.setFrom files(subprojects.collect { project ->
            project.sourceSets.main.output
        })

        sourceDirectories.setFrom files(subprojects.collect { project ->
            project.sourceSets.main.allSource.srcDirs
        })

        executionData.setFrom fileTree(
            dir: rootDir,
            include: '**/build/jacoco/*.exec'
        )
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}


